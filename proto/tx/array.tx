module tx


/** An Array instance contains zero or more elements of the same type serially arranged in memory.
 * Array is the base class for the ARRAY type class.
 * @param <E> The element type
 * @param <C> The array capacity, a UInt value
 */
builtin type ~ Array<E, C : UInt> derives Any, Collection<E>, Updatable<UInt, E>
{
    override equals( other : &Any ) -> Bool {
        ##print( "equals() this type id=" %s _typeid(self) %% " other=" %s _typeid(other) );
        if other is oa : &Array<E> {
            if self.L != oa.L:
                return FALSE;
            for i in 0..self.L:
                if self[i] != oa[i]:
                    return FALSE;
            return TRUE;
        }
        return FALSE;
    }

    override empty() -> Bool {
        return self.L == 0;
    }

    override count() -> Ordinal {
        return self.L;
    }

    override capacity() -> Ordinal {
        return self.C;
    }

    override contains( val : E ) -> Bool {
/* TODO: requires equals() or similar in Any
        for i in 0..self.L-1 {
            if self[i] == val:
                return TRUE;
        }
*/
        return FALSE;
    }

    override add( val : E ) ~ -> Bool {
        self[ self.L ] = val;
        return TRUE;
    }

/* built-in:
    override clear() ~ { ... }
*/



    override sequencer() -> Ref< ~Iterator< E > > {
        return new ~ArrayIterator( self );
    }

    override has( ix : UInt ) -> Bool {
        return ix < self.L;
    }

    override get( ix : UInt ) -> E {
        return self[ ix ];  ## (bounds check is built-in)
    }

    override set( ix : UInt, element : E ) ~ -> E {
        prev := self[ ix ];
        self[ ix ] = element;
        return prev;
    }

    override swap( ixA : UInt, ixB : UInt ) ~ {
        tmp := self[ ixA ];
        self[ ixA ] = self[ ixB ];
        self[ ixB ] = tmp;
    }



    type ~ ArrayIterator <: Tuple, Iterator< E > {
        array     : &Array<E>;
        nextIndex : ~UInt;

        self( array : &Array<E> ) {
            self.array = array;
            self.nextIndex = 0;
        }

        override sequencer() ~ -> Ref< ~ArrayIterator > {
            return self;
        }

        override has_next() -> Bool {
            return self.nextIndex < self.array.L;
        }
    
        override has_prev() -> Bool {
            return self.nextIndex > 0;
        }

        override next() ~ -> E {
            n := self.nextIndex;
            self.nextIndex = self.nextIndex + 1;
            return self.array[ n ];
        }

        override prev() ~ -> E  {
            self.nextIndex = self.nextIndex - 1;
            return self.array[ self.nextIndex ];
        }
    }
}
