
rem( dividend : Long, divisor : Long ) -> Long:
    return dividend - dividend / divisor * divisor

puts(cstr : &[]UByte, f : Long) :
    pstr : ~[cstr.L + 2]UByte = cstr
    pstr[pstr.L-1] = ' '
    pstr[pstr.L] = '0' + UByte( rem( f, 10 ) )
    pstr[pstr.L] = 0
    tx.c.puts( pstr )


type ~ AType derives Tuple :
    virtual SFIELD := 41
    virtual vm_field : ~Float = 3
    virtual vm_field2 : ~Float = 3
    virtual vm_field3 : ~Float = 3

    instance_field : ~Long
    instance_field2 : ~Long

    self() :
        #init: instance_field(1), instance_field2(1)

    print() :
        puts(c"AType", self.instance_field)

    virtual v_method(a : Int)->Int :
        #experr 1: self.instance_field = a  ## virtual methods can't access self / instance members
        return a * 2

    virtual v_method2(a : Int)->Int :
        return a * 2

    virtual v_func_field := (a : Int) -> Int: {
        #experr 1: self.instance_field = a;  ## virtual members can't access self / instance members
        return a*2;
    };

    ## overloaded method name:
    ## TODO: Support this. For now, error is generated.

    #experr: multimethod(a : Int)->Int :
        return Int( a * 3 )

    #experr: multimethod(a : Float)->Int :
        return Int( a * 3.5 )


type ~ BType derives AType :
    virtual override SFIELD := 42
    virtual override vm_field : ~Float = 4
    instance_field : ~Long = 6
    virtual override v_func_field := (a : Int) -> Int: return a*4; ;

    #experr 1: vm_field2 : ~Float = 4                       ## non-virtual though parent field is virtual
    #experr 1: virtual vm_field3 : ~Float = 4               ## missing 'override'
    #experr 1: virtual instance_field2 : ~Long = 100        ## 'virtual' though parent field is non-virtual
    #experr 1: override err_o_field1 : ~Long = 100          ## override on fields only if also virtual
    #experr 1: virtual override err_o_field2 : ~Long = 100  ## override on field though no parent field to override

##    self() :
##        #init: instance_field(3)

    override print() :
        puts(c"BType", self.instance_field)

    virtual override v_method(a : Int)->Int :
        return Super.v_method( a ) * 2

    #experr 1: override v_method2(a : Int)->Int :  ## overrides virtual method but doesn't declare virtual
        return Super.v_method2( a ) * 2

    #experr 1: override bad_method() {}  ## no such parent method

    b_method() : {}

    ## overriding overloaded method (in reverse order):
    ## TODO: Support this.
    /*
    override multimethod(a : Float)->Int :
        return super.multimethod( a ) * 2

    override multimethod(a : Int)->Int :
        return super.multimethod( a ) * 3

    #experr: virtual override multimethod : Bool
    */


main()->Int :
    bo := ~BType()
    ar : &~AType = &bo
    br : &~BType = &bo

    assert ar.SFIELD == 42
    assert br.SFIELD == 42
    assert bo.SFIELD == 42
    assert AType.SFIELD == 41
    assert BType.SFIELD == 42

    assert ar.vm_field == 4
    assert br.vm_field == 4
    assert bo.vm_field == 4
    assert AType.vm_field == 3
    assert BType.vm_field == 4

    ar.instance_field = 10
    br.instance_field = 20
    assert ar.instance_field == 10
    assert br.instance_field == 20
    assert bo.instance_field == 20

    ar.instance_field = 40
    br.instance_field = 80
    assert ar.instance_field == 40
    assert br.instance_field == 80
    assert bo.instance_field == 80

    br.b_method()
    #experr: ar.b_method()

    ## methods are always "virtual":
    ar.instance_field = 4
    br.instance_field = 8
    ##ar.print()
    ##br.print()
    vala := ar.v_method( 2 )
    assert vala == 8
    valb := br.v_method( 2 )
    assert valb == 8
/*
    val := ar.multimethod( 4.0 )
    assert val == 28
    val := ar.multimethod( 4 )
    assert val == 36
*/
    return 0

